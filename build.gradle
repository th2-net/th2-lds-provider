/*
 * Copyright 2009-2023 Exactpro (Exactpro Systems Limited)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.exactpro.th2.common-convensions'
    id 'application'
    id 'java-test-fixtures'
    id 'com.palantir.docker' version '0.25.0'
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
    id 'org.jetbrains.kotlin.kapt'
    id "org.owasp.dependencycheck" version "8.1.2"
    id 'org.jetbrains.kotlin.plugin.serialization' version "1.8.10"
}

ext {
    dockerImageVersion = release_version
    cradleApiVersion = '5.1.0-separate-executor-metrics-5191174758-a60b329-SNAPSHOT'
    commonVersion = '5.2.0-json-raw-body-4958342146-824798d-SNAPSHOT'
    javalin = '5.4.2'
}

dependencyCheck {
    formats=['SARIF', 'JSON', 'HTML']
    failBuildOnCVSS=5

    analyzers {
        assemblyEnabled = false
        nugetconfEnabled = false
        nodeEnabled = false
    }
}

dependencies {
    implementation("com.exactpro.th2:common:${commonVersion}") {
        exclude group: 'com.exactpro.th2', module: 'cradle-core'
        exclude group: 'com.exactpro.th2', module: 'cradle-cassandra'
    }

    implementation "io.javalin:javalin:$javalin"
    implementation "io.javalin:javalin-micrometer:$javalin"

    implementation(platform('io.micrometer:micrometer-bom:1.10.1')) {
        because('should match the version in javalin-micrometer')
    }

    implementation('io.micrometer:micrometer-registry-prometheus')
    implementation 'org.apache.commons:commons-lang3'

    kapt("io.javalin.community.openapi:openapi-annotation-processor:$javalin")

    implementation("io.javalin.community.openapi:javalin-openapi-plugin:$javalin") {
        because("for /openapi route with JSON scheme")
    }
    implementation("io.javalin.community.openapi:javalin-swagger-plugin:$javalin") {
        because("for Swagger UI")
    }
    implementation("io.javalin.community.openapi:javalin-redoc-plugin:$javalin") {
        because("for Re Doc UI")
    }

    implementation("com.exactpro.th2:cradle-cassandra:$cradleApiVersion")
    implementation('net.jpountz.lz4:lz4:1.3.0') {
        because('cassandra driver requires lz4 impl in classpath for compression')
    }
    implementation project(':grpc-lw-data-provider')

    implementation('io.prometheus:simpleclient') {
        because('need add custom metrics to provider')
    }

    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json-jvm:1.5.0'

    implementation 'io.netty:netty-buffer'

    testImplementation "io.javalin:javalin-testtools:$javalin"

    testImplementation(testFixtures("com.exactpro.th2:common:${commonVersion}"))
    testImplementation platform('org.testcontainers:testcontainers-bom:1.18.0')
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation "org.testcontainers:cassandra"

    testImplementation 'com.datastax.oss:java-driver-core'
}

//FIXME: Remove for any release
configurations.configureEach {
    resolutionStrategy {
        force "com.exactpro.th2:cradle-core:$cradleApiVersion"
        force "com.exactpro.th2:cradle-cassandra:$cradleApiVersion"
    }
}

test {
    useJUnitPlatform {
        excludeTags('integration-test')
    }
}

tasks.register('integrationTest', Test) {
    group = 'verification'
    useJUnitPlatform {
        includeTags('integration-test')
    }
    testLogging {
        showStandardStreams = true
    }
}

applicationName = 'service'

distTar {
    archiveFileName.set("${applicationName}.tar")
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}

application {
    mainClass.set('com.exactpro.th2.lwdataprovider.MainKt')
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

// For GitHub only
nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}